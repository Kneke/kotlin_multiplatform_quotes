apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group 'de.cknetsc'
version '0.0.1'

ext {
    kotlinx_coroutines = '1.1.1'
    mockk_version = '1.9.kotlin12'
    kluent_version = '1.47'
}

kotlin {
    targets {
        // This preset is for iPhone emulator
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64
        fromPreset(iOSTarget, 'ios') {
            compilations.main.outputKinds('FRAMEWORK')
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
        }

        fromPreset(presets.jvm, 'jvm')

        fromPreset(presets.android, 'android')
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$kotlinx_coroutines"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$kotlinx_serialization_runtime"

                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-json:$ktor_version"
                implementation "io.ktor:ktor-client-logging:$ktor_version"
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'

                implementation "io.mockk:mockk:$mockk_version"
                implementation "io.mockk:mockk-common:$mockk_version"
                implementation "org.amshove.kluent:kluent:$kluent_version"
            }
        }
        sharedJvm {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_runtime"

                implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
            }
        }
        jvmMain {
            dependsOn sharedJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines"
                implementation "com.squareup.sqldelight:sqlite-driver:$sqldelight_version"

                implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        androidMain {
            dependsOn sharedJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines"
                implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"

                implementation "io.ktor:ktor-client-android:$ktor_version"
            }
        }
        androidTest {
            dependsOn jvmTest
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$kotlinx_coroutines"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$kotlinx_serialization_runtime"
                implementation "com.squareup.sqldelight:ios-driver:$sqldelight_version"

                implementation "io.ktor:ktor-client-ios:$ktor_version"
                implementation "io.ktor:ktor-client-core-native:$ktor_version"
                implementation "io.ktor:ktor-client-json-native:$ktor_version"
                implementation "io.ktor:ktor-client-logging-native:$ktor_version"
            }
        }
        iosTest {

        }
    }
}

android {
    compileSdkVersion android_compileSdkVersion
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
        }
    }
}

sqldelight {
    packageName = "de.cknetsc.multiapp"
    className = "MyDatabase"
    sourceSet = files("src/commonMain/kotlin/sqldelight")
    schemaOutputDirectory = file("src/commonMain/kotlin/sqldelight")
}

configurations {
    compileClasspath
}


task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn kotlin.targets.ios.compilations.main.linkTaskName("FRAMEWORK", mode)

    from { kotlin.targets.ios.compilations.main.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode